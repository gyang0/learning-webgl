<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <title>Brief Timeline of Geography</title>
    </head>

    <style>
        #glCanvas {
            border: 2px solid black;
        }
    </style>

    <canvas id="glCanvas" width="1000px" height = "600px"></canvas>

    <body>
          <!--
            Following the guide in
            https://inspirnathan.com/posts/49-shadertoy-tutorial-part-3
          -->
    </body>

    <script id="vertex-shader" type="glsl">#version 300 es
        in vec4 a_position;

        void main(){
            gl_Position = a_position;
        }
    </script>

    <script id="fragment-shader" type="glsl">#version 300 es
        precision highp float;
        out vec4 outColor;

        uniform vec2 iResolution; // Screen size <x, y>
        uniform float iTime; // Time elapsed since start

        // Raymarching setup
        const int MAX_RAYMARCH_STEPS = 220;
        const float SCENE_START = 0.0;
        const float SCENE_END = 100.0;
        const float EPSILON = 0.001;

        // Material structure
        struct Material {
            vec3 ambientCol;
            vec3 diffuseCol;
            vec3 specularCol;
            float alpha; // Shininess
        };

        // Surface structure
        struct Surface {
            float dist; // Distance to shape
            Material mat; // Material of surface
        };

        Material gold(){
            return Material(
                0.5 * vec3(0.7, 0.5, 0),
                0.6 * vec3(0.7, 0.7, 0),
                0.6 * vec3(1),
                5.0
            );
        }

        Material silver(){
            return Material(
                0.4 * vec3(0.8),
                0.5 * vec3(0.7),
                0.6 * vec3(1),
                5.0
            );
        }

        Material board(vec3 p){
            return Material(
                0.3 * vec3(1.0 + 0.7*mod(floor(p.x) + floor(p.z), 2.0)),
                vec3(0.3),
                vec3(0),
                1.0
            );
        }


        // Floor
        float sdfFloor(vec3 p){
            return p.y + 1.0;
        }

        // SDF for a sphere
        float sdfSphere(vec3 p, float r, vec3 offset){
            return length(p - offset) - r;
        }

        // Min function based on distance that preserves color too.
        Surface minWithCol(Surface obj1, Surface obj2){
            if(obj2.dist < obj1.dist) return obj2;
            else return obj1;
        }

        // SDF world, contains all shapes.
        Surface sdfScene(vec3 p){
            Surface floor = Surface(sdfFloor(p), board(p));

            Surface sphere0 = Surface(sdfSphere(p, 0.8, vec3(-2, 0, 0)), gold());
            Surface sphere1 = Surface(sdfSphere(p, 1.2, vec3(2, 1, 0)), silver());

            return minWithCol(floor, minWithCol(sphere0, sphere1));
        }

        // Calculate the normal of a sphere
        vec3 calcNormal(vec3 p){
            vec2 e = vec2(1.0, -1.0) * EPSILON;

            return normalize(
                e.xyy * sdfScene(p + e.xyy).dist + 
                e.yyx * sdfScene(p + e.yyx).dist + 
                e.yxy * sdfScene(p + e.yxy).dist + 
                e.xxx * sdfScene(p + e.xxx).dist
            );
        }

        // Raymarching function
        Surface rayMarch(vec3 ro, vec3 rd){
            float totalDist = SCENE_START;
            Surface closest;

            for(int i = 0; i < MAX_RAYMARCH_STEPS; i++){
                vec3 p = ro + totalDist*rd;
                
                closest = sdfScene(p);
                totalDist += closest.dist;

                if(closest.dist < EPSILON || totalDist > SCENE_END)
                    break;
            }

            return Surface(totalDist, closest.mat);
        }

        vec3 phong(vec3 lightDir, vec3 normal, vec3 rd, Material mat){
            // Ambient
            vec3 ambient = mat.ambientCol;

            // Diffuse
            vec3 diffuse = clamp(dot(lightDir, normal), 0.0, 1.0) * mat.diffuseCol;

            // Specular
            float dotRV = clamp(dot(reflect(lightDir, normal), -rd), 0.0, 1.0);
            vec3 specular = pow(dotRV, mat.alpha) * mat.specularCol;

            return ambient + diffuse + specular;
        }

        float softShadow(vec3 ro, vec3 rd, float mint, float maxt){
            float res = 1.0;
            float t = mint;

            for(int i = 0; i < 16; i++){
                float h = sdfScene(ro + rd * t).dist;
                res = min(res, 2.5 * h/t);
                t += clamp(h, 0.02, 0.1);

                if(h < EPSILON || t > maxt) break;
            }

            return clamp(res, 0.0, 1.0);
        }

        void main(){
            // Get view
            vec2 uv = gl_FragCoord.xy/iResolution.xy;
            uv -= 0.5;
            uv.x *= iResolution.x/iResolution.y;

            // Initial color of the scene
            vec3 backgroundCol = vec3(0.2);
            vec3 col = vec3(0);

            // Ray origin and ray direction
            vec3 ro = vec3(0, 0, 5);
            vec3 rd = normalize(vec3(uv, -1));

            // Raymarch
            Surface closest = rayMarch(ro, rd);

            if(closest.dist > SCENE_END){
                col = backgroundCol;

            } else{
                // Point on sphere
                vec3 pos = ro + rd * closest.dist;

                // Surface normal estimation
                vec3 normal = calcNormal(pos);

                // Light position #1
                vec3 lightPos = vec3(cos(iTime) + 0.0, 2.0, 3.0 + sin(iTime));
                vec3 lightDir = normalize(lightPos - pos);
                float lightIntensity = 0.6;

                // Soft shadows
                float shadow = clamp(softShadow(pos, lightDir, 0.02, 2.5), 0.2, 1.0);

                // Final color
                col = lightIntensity * phong(lightDir, normal, rd, closest.mat);
                col *= shadow;

                // Background fog
                col = mix(col, backgroundCol, 1.0 - exp(-EPSILON * closest.dist*closest.dist*closest.dist));

                // Gamma correction
                //col = pow(col, vec3(1.0/2.2));
            }

            outColor = vec4(col, 1.0);
        }
    </script>

    <!-- Program -->
    <script>
        function createShader(gl, type, source){
            var shader = gl.createShader(type);
            gl.shaderSource(shader, source);
            gl.compileShader(shader);

            var success = gl.getShaderParameter(shader, gl.COMPILE_STATUS);
            if(!success){
                console.log("createShader(): " + gl.getShaderInfoLog(shader));
                return undefined;
            }

            return shader;
        }

        function createProgram(gl, vertexShader, fragmentShader){
            var program = gl.createProgram();
            gl.attachShader(program, vertexShader);
            gl.attachShader(program, fragmentShader);
            gl.linkProgram(program);

            var success = gl.getProgramParameter(program, gl.LINK_STATUS);
            if(!success){
                console.log("createProgram(): " + gl.getProgramInfoLog(program));
                return undefined;
            }

            return program;
        }

        // Canvas
        var canvas = document.querySelector("#glCanvas");
        var gl = canvas.getContext("webgl2");
        if(!gl){
            console.log("No WebGL");
        } else {
            // Create shaders
            var vertexShaderSource = document.querySelector("#vertex-shader").text;
            var fragmentShaderSource = document.querySelector("#fragment-shader").text;

            var vertexShader = createShader(gl, gl.VERTEX_SHADER, vertexShaderSource);
            var fragmentShader = createShader(gl, gl.FRAGMENT_SHADER, fragmentShaderSource);

            // Link shaders
            var program = createProgram(gl, vertexShader, fragmentShader);

            var positionAttribLocation = gl.getAttribLocation(program, "a_position");
            var positionBuffer = gl.createBuffer();

            gl.bindBuffer(gl.ARRAY_BUFFER, positionBuffer);

            var positions = [
                -1, -1,  1, -1,  1,  1,
                -1, -1,  1,  1, -1,  1
            ];
            gl.bufferData(gl.ARRAY_BUFFER, new Float32Array(positions), gl.STATIC_DRAW);

            var vao = gl.createVertexArray();
            gl.bindVertexArray(vao);
            gl.enableVertexAttribArray(positionAttribLocation);
            gl.vertexAttribPointer(positionAttribLocation, 2, gl.FLOAT, false, 0, 0);

            // Canvas
            gl.viewport(0, 0, gl.canvas.width, gl.canvas.height);

            gl.useProgram(program);
            gl.bindVertexArray(vao);


            // Set screen size
            var iResolutionLoc = gl.getUniformLocation(program, "iResolution");
            gl.uniform2f(iResolutionLoc, canvas.width, canvas.height);

            // For setting the time elapsed
            var iTimeLoc = gl.getUniformLocation(program, "iTime");

            function render(t){
                gl.clearColor(0, 0, 0, 0);
                gl.clear(gl.COLOR_BUFFER_BIT);
                gl.drawArrays(gl.TRIANGLES, 0, 6);

                // Set time elapsed
                gl.uniform1f(iTimeLoc, t/1000.0);

                window.requestAnimationFrame(render);
            }
            window.requestAnimationFrame(render);
        }
        
    </script>
</html>
