<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <title>Utility Functions</title>
    </head>

    <style>
        #glCanvas {
            border: 2px solid black;
        }
    </style>

    <canvas id="glCanvas" width="800px" height = "600px"></canvas>

    <body>
        <!--
            My attempt to write custom noise and hash functions (2D and 3D)
            Doesn't work as well as other methods, but at least it looks okay.

            Tester code based on:
                https://inspirnathan.com/posts/49-shadertoy-tutorial-part-3
         -->

    </body>

    <script id="vertex-shader">#version 300 es
        in vec4 a_position;

        void main(){
            gl_Position = a_position;
        }
    </script>

    <script id="fragment-shader">#version 300 es
        precision highp float;
        out vec4 outColor;

        uniform vec2 u_resolution; // Screen size <x, y>
        uniform float u_time; // Time elapsed since start

        
        // ---------------------------------
        // START OF UTILITY FUNCTIONS
        // ---------------------------------
        

        // 2D random method - a weird mix of fractions and trig
        float rand2d(in vec2 p){
            float a = 208.17 * fract(p.x*153.202 + p.y*36.14);
            return clamp(fract(sin(a) * 511.12), 0.0, 1.0);
        }

        // 3D random method
        float rand3d(in vec3 p){
            float a = 208.17 * fract(p.x*153.202 + p.z*36.14);
            return clamp(fract(sin(a) * 511.12), 0.0, 1.0);
        }

        // 2D perlin noise using interpolation between square coordinates
        float perlin2d(in vec2 p){
            vec2 pos = floor(p);
            vec2 frac = fract(p);

            vec2 s = smoothstep(0.0, 1.0, frac);

            // The usual: 4 corners and interpolate.
            float c0 = rand2d(pos + vec2(0.0, 0.0)); // bottom left (original point)
            float c1 = rand2d(pos + vec2(1.0, 0.0)); // bottom right
            float c2 = rand2d(pos + vec2(0.0, 1.0)); // top left
            float c3 = rand2d(pos + vec2(1.0, 1.0)); // top right

            // The math here is probably wrong, but it's simple and it looks okay.
            // i1 is the interpolation between c0 and c1
            // i2 is the interpolation between c2 and c3.
            // Then I interpolate those results with weight s.y (best seen by drawing a diagram)
            float i1 = mix(c0, c1, s.x);
            float i2 = mix(c2, c3, s.x);

            return mix(i1, i2, s.y);
        }

        // An attempt at 3D perlin noise using trilinear interpolation
        float perlin3d(in vec3 p){
            vec3 pos = floor(p);
            vec3 frac = fract(p);

            // Same as 2d perlin, but with a cube instead of a square.
            float c0 = rand3d(pos + vec3(0, 0, 0));
            float c1 = rand3d(pos + vec3(1, 0, 0));
            float c2 = rand3d(pos + vec3(0, 0, 1));
            float c3 = rand3d(pos + vec3(1, 0, 1));

            float c4 = rand3d(pos + vec3(0, 1, 0));
            float c5 = rand3d(pos + vec3(1, 1, 0));
            float c6 = rand3d(pos + vec3(0, 1, 1));
            float c7 = rand3d(pos + vec3(1, 1, 1));

            vec3 s = smoothstep(0.0, 1.0, frac);

            float plane1 = mix(
                mix(c0, c1, s.x),
                mix(c4, c5, s.x),
                s.y
            );

            float plane2 = mix(
                mix(c2, c3, s.x),
                mix(c6, c7, s.x),
                s.y
            );

            return mix(plane1, plane2, s.z);
        }

        // Fractional brownian motion (doesn't work that well)
        // Needs black specular to look good.
        // Octaves in range 1-4 will do.
        float fbm(in vec3 p, int octaves){
            float total = 0.0;
            float freq = 1.0;
            float amplitude = 1.0;

            // Rotation of 30 degrees around y-axis
            // WebGL matrix format != math matrix format
            const mat3 rotMat = mat3( 0.86, 0.00, -0.50,
              0.00, 1.00, 0.00,
              0.50, 0.00, 0.86);

            for(int i = 0; i < octaves; i++){
                total += perlin3d(p * freq) * amplitude;

                freq *= 2.0;
                amplitude *= 0.5;

                p *= rotMat;
            }

            return total;
        }


        // ---------------------------------
        // END OF UTILITY FUNCTIONS
        // TEST PROGRAM BELOW
        // ---------------------------------


        // Raymarching setup
        const int MAX_RAYMARCH_STEPS = 220;
        const float SCENE_START = 0.0;
        const float SCENE_END = 100.0;
        const float EPSILON = 0.001;

        // Material structure
        struct Material {
            vec3 ambientCol;
            vec3 diffuseCol;
            vec3 specularCol;
            float alpha; // Shininess
        };

        // Surface structure
        struct Surface {
            float dist; // Distance to shape
            Material mat; // Material of surface
        };

        Material gold(vec3 p){
            return Material(
                vec3(0),
                vec3(0.6, 0.2, 0.2),
                vec3(0.1),
                100.0
            );
        }

        Material silver(vec3 p){
            return Material(
                0.4 * vec3(0.8),
                vec3(0.8),
                0.6 * vec3(1),
                5.0
            );
        }

        Material board(vec3 p){
            return Material(
                0.3 * vec3(1.0 + 0.7*mod(floor(p.x) + floor(p.z), 2.0)),
                vec3(0.3),
                vec3(0),
                1.0
            );
        }


        // Floor
        float sdfFloor(vec3 p){
            return p.y + 0.5 + fbm(p, 4);
        }

        // SDF for a sphere
        float sdfSphere(vec3 p, float r, vec3 offset){
            return length(p - offset) - r;
        }

        // Min function based on distance that preserves color too.
        Surface minWithCol(Surface obj1, Surface obj2){
            if(obj2.dist < obj1.dist) return obj2;
            else return obj1;
        }

        // SDF world, contains all shapes.
        Surface sdfScene(vec3 p){
            Surface floor = Surface(sdfFloor(p), gold(p));

            Surface sphere0 = Surface(sdfSphere(p, 0.8, vec3(-2, 0, 0)), gold(p));
            Surface sphere1 = Surface(sdfSphere(p, 1.2, vec3(2, 1, 0)), silver(p));

            return minWithCol(floor, minWithCol(sphere0, sphere1));
        }

        // Calculate the normal of a sphere
        vec3 calcNormal(vec3 p){
            vec2 e = vec2(1.0, -1.0) * EPSILON;

            return normalize(
                e.xyy * sdfScene(p + e.xyy).dist + 
                e.yyx * sdfScene(p + e.yyx).dist + 
                e.yxy * sdfScene(p + e.yxy).dist + 
                e.xxx * sdfScene(p + e.xxx).dist
            );
        }

        // Raymarching function
        Surface rayMarch(vec3 ro, vec3 rd){
            float totalDist = SCENE_START;
            Surface closest;

            for(int i = 0; i < MAX_RAYMARCH_STEPS; i++){
                vec3 p = ro + totalDist*rd;
                
                closest = sdfScene(p);
                totalDist += closest.dist;

                if(closest.dist < EPSILON || totalDist > SCENE_END)
                    break;
            }

            return Surface(totalDist, closest.mat);
        }

        vec3 phong(vec3 lightDir, vec3 normal, vec3 rd, Material mat){
            // Ambient
            vec3 ambient = mat.ambientCol;

            // Diffuse
            vec3 diffuse = clamp(dot(lightDir, normal), 0.0, 1.0) * mat.diffuseCol;

            // Specular
            float dotRV = clamp(dot(reflect(lightDir, normal), -rd), 0.0, 1.0);
            vec3 specular = pow(dotRV, mat.alpha) * mat.specularCol;

            return ambient + diffuse + specular;
        }

        float softShadow(vec3 ro, vec3 rd, float mint, float maxt){
            float res = 1.0;
            float t = mint;

            for(int i = 0; i < 16; i++){
                float h = sdfScene(ro + rd * t).dist;
                res = min(res, 2.5 * h/t);
                t += clamp(h, 0.02, 0.1);

                if(h < EPSILON || t > maxt) break;
            }

            return clamp(res, 0.0, 1.0);
        }

        void main(){
            // Get view
            vec2 uv = gl_FragCoord.xy/u_resolution.xy;
            uv -= 0.5;
            uv.x *= u_resolution.x/u_resolution.y;

            // Initial color of the scene
            vec3 backgroundCol = vec3(0.2);
            vec3 col = vec3(0);

            // Ray origin and ray direction
            vec3 ro = vec3(0, 0, 5);
            vec3 rd = normalize(vec3(uv, -1));

            // Raymarch
            Surface closest = rayMarch(ro, rd);

            if(closest.dist > SCENE_END){
                col = backgroundCol;

            } else {
                // Point on sphere
                vec3 pos = ro + rd * closest.dist;

                // Surface normal estimation
                vec3 normal = calcNormal(pos);

                // Light position #1
                vec3 lightPos = vec3(2.0, 3.0, 10.0);
                vec3 lightDir = normalize(lightPos - pos);
                float lightIntensity = 0.5;

                // Soft shadows
                float shadow = clamp(softShadow(pos, lightDir, 0.02, 2.5), 0.9, 1.0);

                // Final color
                col = lightIntensity * phong(lightDir, normal, rd, closest.mat);
                col *= shadow;

                // Background fog
                col = mix(col, backgroundCol, 1.0 - exp(-EPSILON * closest.dist*closest.dist*closest.dist));

                // Gamma correction
                //col = pow(col, vec3(1.0/2.2));
            }

            outColor = vec4(col, 1.0);
        }
    </script>

    <!-- Program -->
    <script>
        function createShader(gl, type, source){
            var shader = gl.createShader(type);
            gl.shaderSource(shader, source);
            gl.compileShader(shader);

            var success = gl.getShaderParameter(shader, gl.COMPILE_STATUS);
            if(!success){
                console.log("createShader(): " + gl.getShaderInfoLog(shader));
                return undefined;
            }

            return shader;
        }

        function createProgram(gl, vertexShader, fragmentShader){
            var program = gl.createProgram();
            gl.attachShader(program, vertexShader);
            gl.attachShader(program, fragmentShader);
            gl.linkProgram(program);

            var success = gl.getProgramParameter(program, gl.LINK_STATUS);
            if(!success){
                console.log("createProgram(): " + gl.getProgramInfoLog(program));
                return undefined;
            }

            return program;
        }

        // Canvas
        var canvas = document.querySelector("#glCanvas");
        var gl = canvas.getContext("webgl2");
        if(!gl){
            console.log("No WebGL");
        } else {
            // Create shaders
            var vertexShaderSource = document.querySelector("#vertex-shader").text;
            var fragmentShaderSource = document.querySelector("#fragment-shader").text;

            var vertexShader = createShader(gl, gl.VERTEX_SHADER, vertexShaderSource);
            var fragmentShader = createShader(gl, gl.FRAGMENT_SHADER, fragmentShaderSource);

            // Link shaders
            var program = createProgram(gl, vertexShader, fragmentShader);

            var positionAttribLocation = gl.getAttribLocation(program, "a_position");
            var positionBuffer = gl.createBuffer();

            gl.bindBuffer(gl.ARRAY_BUFFER, positionBuffer);

            var positions = [
                -1, -1,  1, -1,  1,  1,
                -1, -1,  1,  1, -1,  1
            ];
            gl.bufferData(gl.ARRAY_BUFFER, new Float32Array(positions), gl.STATIC_DRAW);

            var vao = gl.createVertexArray();
            gl.bindVertexArray(vao);
            gl.enableVertexAttribArray(positionAttribLocation);
            gl.vertexAttribPointer(positionAttribLocation, 2, gl.FLOAT, false, 0, 0);

            // Canvas
            gl.viewport(0, 0, gl.canvas.width, gl.canvas.height);

            gl.useProgram(program);
            gl.bindVertexArray(vao);


            // Set screen size
            var u_resolutionLoc = gl.getUniformLocation(program, "u_resolution");
            gl.uniform2f(u_resolutionLoc, canvas.width, canvas.height);

            // For setting the time elapsed
            var u_timeLoc = gl.getUniformLocation(program, "u_time");

            function render(/*t*/){
                gl.clearColor(0, 0, 0, 0);
                gl.clear(gl.COLOR_BUFFER_BIT);
                gl.drawArrays(gl.TRIANGLES, 0, 6);

                // Set time elapsed
                //gl.uniform1f(u_timeLoc, t/1000.0);

                //window.requestAnimationFrame(render);
            }
            render();
            //window.requestAnimationFrame(render);
        }
        
    </script>
</html>
